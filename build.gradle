//
// ============================================================================
// (C) Copyright Schalk W. Cronje 2013-2015
//
// This software is licensed under the Apache License 2.0
// See http://www.apache.org/licenses/LICENSE-2.0 for license details
//
// Unless required by applicable law or agreed to in writing, software distributed under the License is
// distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and limitations under the License.
//
// ============================================================================
//

// PLEASE NOTE: Integration Tests and Gradle Compatibility tests download a number of files.

buildscript {
    repositories {
        jcenter()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
        classpath 'org.ysb33r.gradle:gradletest:0.5.3'

        // See: https://github.com/hierynomus/license-gradle-plugin/issues/67
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0', {
            exclude module : 'guava'
        }
    }
}

apply plugin : 'groovy'
apply plugin : 'maven'
apply plugin : 'com.gradle.plugin-publish'
apply plugin : 'com.github.hierynomus.license'
apply plugin : 'org.ysb33r.gradletest'

apply from: 'gradle/integration-tests.gradle'

sourceCompatibility = 1.6
targetCompatibility = 1.6

version = '0.3.5-SNAPSHOT'
group = 'org.ysb33r.gradle'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3', {
        exclude module : 'groovy-all'
    }
}

integrationTest {
    systemProperties TESTROOT : buildDir

    if(gradle.startParameter.isOffline()) {
        systemProperties OFFLINE: 1
    }
}

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = false
    mapping {
        groovy ='DOUBLESLASH_STYLE'
    }
    ext.year = '2013-2015'
    excludes(['**/*.ad', '**/*.asciidoc', '**/*.adoc', '**/*.md','**/*.properties'])
}

pluginBundle {
    description = 'A plugin that keeps a local repository for offline of isolated environments'
    website     = 'https://github.com/ysb33r/ivypot-gradle-plugin'
    vcsUrl      = 'https://github.com/ysb33r/ivypot-gradle-plugin.git'

    tags = ['repositoryManagement','repositories']

    plugins {
        ivypotPlugin {
            id = 'org.ysb33r.ivypot'
            displayName = 'Plugin for maintaining an offline respository'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = 'ivypot'
    }

}

publishPlugins {
    onlyIf { !version.endsWith("SNAPSHOT") }
}

gradleLocations {
    downloadToGradleUserHome = true
}

gradleTest {
    versions '2.0'
    versions '2.2'
    versions '2.3'
    versions '2.4'

    inputs.files jar
}